# Haste. Beserker class's first active.
on rightclick holding a rotten flesh:

        # Check if the player has the class node.
        if player has permission "skript.beserker":

                # Check if the player is wearing the armour
                player is wearing a leather leggings and leather chestplate:

                        # Make a local variable storing the time difference.
                        set {_waited} to difference between {classability1-%player%} and now

                        # Ability on cooldown.
                        if {_waited} is smaller than 30 seconds:
                                send "&8[&9Skript&8] [&dBeserker&8] [&6&lWarn&8]&7 This ability is on cooldown for another %difference between 30 seconds and {_waited}%&7!"
                                stop
                        # Run ability.
                        else:
                                send "&8[&9Skript&8] [&dBeserker&8] [&a&lSuccess&8]&7 You've used haste!"
                                set {classability1-%player%} to now
                                apply speed 2 to player for 10 seconds

                # Player is not wearing the right armour.
                else:
                        send "&8[&9Skript&8] [&dBeserker&8] [&6&lWarn&8]&7 You must use your class armour in order to use abilities!"

        # If they don't have permission reply that they don't.
        else:
                send "&8[&9Skript&8] [&dBeserker&8] [&6&lWarn&8]&7 You do not have this class and thus cannot use their ability."

# Rage. Beserker class's second active.
on leftclick holding rotten flesh:

        # Check if the player has the class node.
        if player has permission "skript.beserker":

                # Check if the player is wearing the armour.
                player is wearing a leather leggings and leather chestplate:

                        # Make a local variable storing the time difference.
                        set {_waited} to difference between {classability2-%player%} and now

                        # Ability on cooldown.
                        if {_waited} is smaller than a minute:
                                send "&8[&9Skript&8] [&dBeserker&8] [&6&lWarn&8]&7 This ability is on cooldown for another %difference between a minute and {_waited}%&7!"
                                stop

                        # Run ability.
                        else:
                                send "&8[&9Skript&8] [&dBeserker&8] [&a&lSuccess&8]&7 You've used rage!"
                                apply strength 2 to player for 10 seconds
                                set {classability2-%player%} to now

                # Tell the player if they're not wearing the armour.
                else:
                        send "&8[&9Skript&8] [&dBeserker&8] [&6&lWarn&8]&7 You must use your class armour in order to use abilities!"

# Undying. Beserker class's first passive.
on sprint toggle:

       # Check for class and armour
       if player has permission "skript.beserker":
              player is wearing a leather leggings and leather chestplate:
                     # Apply the effect
                     apply resistance 1 to player for 999999 seconds
                     stop

              # The player has removed their armour and thus we shall remove their bonus.
              remove resistance from player

# Axe Rule. Beserkers can only deal damage with axe.
on damage :

       # Check if player is a beserker
       if attacker has permission "skript.beserker":

              attacker is holding any axe:
                     damage victim by 2 hearts
                     send "&8[&9Skript&8] [&dBeserker&8] [&6&lWarn&8]&7 You take additional damage from the axe wielding beserker!" to victim
                     stop

              cancel event
              send "&8[&9Skript&8] [&dBeserker&8] [&6&lWarn&8]&7 As a beserker you can only deal damage with axes!" to attacker
              send "&8[&9Skript&8] [&dBeserker&8] [&a&lSuccess&8]&7 As your attacker is not using an axe you take no damage." to victim
