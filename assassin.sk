# Invisibility. Assassin class's first active.
on rightclick holding a ghast tear:

        # Check if the player has the class node.
        if player has permission "skript.assassin":

                # Check if the player is wearing the armour
                player is wearing all leather armour:

                        # Make a local variable storing the time difference.
                        set {_waited} to difference between {classability1-%player%} and now

                        # Ability on cooldown.
                        if {_waited} is smaller than 30 seconds:
                                send "&8[&9Skript&8] [&dAssassin&8] [&6&lWarn&8]&7 This ability is on cooldown for another %difference between 30 seconds and {_waited}%&7!"
                                stop
                        # Run ability.
                        else:
                                send "&8[&9Skript&8] [&dAssassin&8] [&a&lSuccess&8]&7 You've used invisibility!"
                                set {classability1-%player%} to now
                                apply invisibility 1 to player for 10 seconds

                # Player is not wearing the right armour.
                else:
                        send "&8[&9Skript&8] [&dAssassin&8] [&6&lWarn&8]&7 You must use your class armour in order to use abilities!"

        # If they don't have permission reply that they don't.
        else:
                send "&8[&9Skript&8] [&dAssassin&8] [&6&lWarn&8]&7 You do not have this class and thus cannot use their ability."

# Poisoned Blade. Assassin class's second active.
on leftclick holding a ghast tear:

        # Check if the player has the class node.
        if player has permission "skript.assassin":

                # Check if the player is wearing the armour.
                player is wearing all leather armour:

                        # Make a local variable storing the time difference.
                        set {_waited} to difference between {classability2-%player%} and now

                        # Ability on cooldown.
                        if {_waited} is smaller than a minute:
                                send "&8[&9Skript&8] [&dAssassin&8] [&6&lWarn&8]&7 This ability is on cooldown for another %difference between a minute and {_waited}%&7!"
                                stop

                        # Run ability.
                        else:
                                send "&8[&9Skript&8] [&dAssassin&8] [&a&lSuccess&8]&7 You've used poisoned blade!"
                                set {classability2-%player%} to now
                                stop

                # Tell the player if they're not wearing the armour.
                else:
                        send "&8[&9Skript&8] [&dAssassin&8] [&6&lWarn&8]&7 You must use your class armour in order to use abilities!"

# Poisoned Blade's Passive Effect. Assassin class's second active.
on damage:

        # Check if the player has the class node.
        if attacker has permission "skript.assassin":

                # Check time passed since ability use.
                if difference between {classability2-%attacker%} and now is smaller than 10 seconds:

                        # Apply the effects
                        apply slowness 1 to victim for 4 seconds
                        apply poison 2 to victim for 4 seconds
                        apply blindness 1 to victim for 4 seconds

                        # Let the victim and attacker know what's going on.
                        send "&8[&9Skript&8] [&dAssassin&8] [&6&lWarn&8]&7 You've been poisoned!" to victim
                        send "&8[&9Skript&8] [&dAssassin&8] [&a&lSuccess&8]&7 You've poisoned your target!" to attacker

                # Bonus damage for assassins.
                damage victim by 1 heart

# Swiftness. Assassin class's first passive.
on sprint toggle:

       # Check for class and armour
       if player has permission "skript.assassin":
              player is wearing all leather armour:
                     # Apply the effect
                     apply swiftness 1 to player for 999999 seconds
                     stop

              # The player has removed their armour and thus we shall remove their bonus.
              remove swiftness from player

# Agility. Assassin class's second passive.
on damage:

        # Check if the player has the permission node.
        if victim has permission "skript.assassin":
                victim is wearing all leather armour:

                        # Not the implementation I'd like! But it should work
                        if difference between {classability1-%victim%} and now is smaller than 10 seconds:
                                chance of 20%:
                                        cancel the event

                                        # Tell the players what happened
                                        send "&8[&9Skript&8] [&dAssassin&8] [&a&lSuccess&8]&7 You've dodged the attack!" to victim
                                        send "&8[&9Skript&8] [&dAssassin&8] [&6&lWarn&8]&7 Your attack has been dodged!" to attacker
                        else:
                                # The assassin's agility negates some of the damage.
                                heal victim by 1
